json = element;

value = object
      | array
      | string
      | number
      | 'true'
      | 'false'
      | 'null';

object = '{', members, '}'
       | '{', ws, '}';

members = member, ',', members
        | member;

member = ws, string, ws, ':', element;

array = '[', elements, ']'
      | '[', ws, ']';

elements = element, ',', elements
         | element;

element = ws, value, ws;

string = '"', characters, '"';

characters = character, characters
           | eps;

character = 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j' | 'k'
          | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' | 'v'
          | 'w' | 'x' | 'y' | 'z'
          | 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J' | 'K'
          | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T' | 'U' | 'V'
          | 'W' | 'X' | 'Y' | 'Z'
          | digit
          | '\x0020'
          | '!' | '#' | '$' | '%' | '&' | "'" | '(' | ')' | '*' | '+' | ','
          | '-' | '.' | '/' | ':' | ';' | '<' | '=' | '>' | '?' | '@' | '['
          | ']' | '{' | '}' | '|' | '~'
          | '\x007f'
          | '\\', escape;

escape = '"' | '\\' | '/' | 'b' | 'f' | 'n' | 'r' | 't'
       | 'u', hex, hex, hex;

hex = digit
     | 'a' | 'b' | 'c' | 'd' | 'e' | 'f'
     | 'A' | 'B' | 'C' | 'D' | 'E' | 'F';

number = integer, fraction, exponent;

integer = '-', onenine, digits
        | onenine, digits
        | '-', digit
        | digit;

digits = digit, digits
       | digit;

digit = '0' | onenine;

onenine = '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9';

fraction = '.', digits | eps;

exponent = 'e', sign, digits
         | 'E', sign, digits
         | eps;

sign = '+' | '-' | eps;

ws = '\x0020', ws
   | '\n', ws
   | '\r', ws
   | '\t', ws
   | eps;
